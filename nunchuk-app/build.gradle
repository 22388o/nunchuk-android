apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: '../configs/dependencies.gradle'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion androidConfig.compileSdkVersion
    ndkVersion '21.0.6113669'

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId androidConfig.applicationId
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode androidConfig.versionCode
        versionName androidConfig.versionName
        multiDexEnabled true
        ndk {
            //abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
            abiFilters 'arm64-v8a'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
            applicationIdSuffix ".dev"
        }
        release {
            minifyEnabled true
            debuggable false
            multiDexKeepFile file('multidex-config.txt')
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.conf'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/versions/9/module-info.class'
    }

    signingConfigs {
        debug {
            storeFile file("${project.rootDir}/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinStdlibVersion"

    implementation project(':nunchuk-arch')
    implementation project(':nunchuk-auth')
    implementation project(':nunchuk-core')
    implementation project(':nunchuk-database')
    implementation project(':nunchuk-main')
    implementation project(':nunchuk-domain')
    implementation project(':nunchuk-network')
    implementation project(':nunchuk-signer')
    implementation project(':nunchuk-wallet')
    implementation project(':nunchuk-widget')
    implementation project(':nunchuk-transaction')
    implementation project(':nunchuk-messages')
    implementation project(':nunchuk-utils')

    implementation nunchukDeps.nativeSdk
    implementation matrixDeps.matrixSdk
    implementation workMangerDeps.values()

    implementation platform(playServices.firebaseBom)
    implementation playServices.firebaseCrashlytics
    implementation playServices.firebaseAnalytics

    implementation lifecycleDeps.values()

    // Dagger
    kapt daggerDeps.compiler
    api daggerDeps.dagger
    kapt daggerDeps.processor
    implementation daggerDeps.support
    implementation daggerDeps.jbAnnotations
    implementation reactiveDeps.rxAndroid2

    implementation networkDeps.values()

    implementation androidxDeps.values()
    implementation googleDeps.values()

    debugImplementation leakDetectionDeps.leakCanary

    debugImplementation debugDatabaseDeps.values()

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
}
