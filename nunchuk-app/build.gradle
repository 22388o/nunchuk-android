apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: '../configs/dependencies.gradle'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion androidConfig.compileSdkVersion
    ndkVersion '21.0.6113669'

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId androidConfig.applicationId
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode androidConfig.versionCode
        versionName androidConfig.versionName
        multiDexEnabled true
        ndk {
            'x86_64'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
            applicationIdSuffix ".dev"
        }
        release {
            minifyEnabled true
            debuggable false
            multiDexKeepFile file('multidex-config.txt')
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.conf'
        }
    }

    flavorDimensions 'cpuArch'
    productFlavors {
        arm8 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
        arm64_v8a {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        x86_64 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'x86_64'
            }
        }
        x86 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'x86'
            }
        }
        universal {
            dimension 'cpuArch'
            // include all default ABIs. with NDK-r16,  it is:
            //   armeabi-v7a, arm64-v8a, x86, x86_64
        }

    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/versions/9/module-info.class'
    }

    signingConfigs {
        debug {
            storeFile file("${project.rootDir}/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinStdlibVersion"

    implementation project(':nunchuk-arch')
    implementation project(':nunchuk-auth')
    implementation project(':nunchuk-core')
    implementation project(':nunchuk-database')
    implementation project(':nunchuk-model')
    implementation project(':nunchuk-nativelib')
    implementation project(':nunchuk-network')
    implementation project(':nunchuk-widget')

    implementation platform(playServices.firebaseBom)
    implementation playServices.firebaseCrashlytics
    implementation playServices.firebaseAnalytics

    // Dagger
    kapt daggerDeps.compiler
    api daggerDeps.dagger
    kapt daggerDeps.processor
    implementation daggerDeps.support
    implementation daggerDeps.jbAnnotations
    implementation reactiveDeps.rxAndroid2
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    implementation androidxDeps.values()
    implementation googleDeps.values()

    debugImplementation leakDetectionDeps.leakCanary

    debugImplementation debugDatabaseDeps.values()
}
